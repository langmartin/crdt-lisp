(in-package :crdt-lisp/anti-entropy)

(defun make-store ()
  (fset:empty-map))

(defun put (store key hlc value)
  (let ((prev (fetch-pair store key)))
    (if (or (not prev)
            (string-lessp (car prev) hlc))
        (fset:map ($ store) (key (cons hlc value)))
        store)))

(defun fetch-pair (store key)
  (fset:lookup store key))

(defun fetch (store key)
  (let ((pair (fetch-pair store key)))
    (if (not pair)
        nil
        (cdr pair))))

;;; ========================================================================
;;; Hash key-hulc pairs and xor them together

(defun key-octets (key)
  (-> (cond ((stringp key) key)
            ((listp key) (format nil "窿脲┅ㄢ徕屐后趄轭绛麸镢翦趔┅ㄤ彐躅桴煦镢翦趔ㄨ蹯悱篝蜷铉躅舛桴煦篝蜷铉┅ㄤ彐躅栳箬镱ㄩ翦愆ō揪ㄣ镱汜翦钺翦Ж鲥泗矧躅箝珙邃怡翦俯脲镢翦趔ㄣ狎轸屙┅ㄨ蹯悱镢翦趔ㄣ徜轸屙┅ㄩ蝻钽灬浜溟珏篝箦聃孱沐后栳驳订镢翦趔鹃铘┅ㄤ彐躅镢翦趔鹃铘镢翦趔ㄣ飙轭翕翦蠛镢翦趔鹃铘镢翦趔戾铉翳镢翦趔┅ㄤ彐躅栳箬矧ㄨ狍桢螬蝈漉沐＇祜瑛矧栳箬弩┅ㄤ彐躅栳箬瞽怡翦ㄨ狍瑭ō栳箬轭翦珏颦戾铉翳ǒ俯沐殪轭绌ㄤ彐躅栳箬篝蜷铉ㄨ狍瑭ㄩ铛祆栳箬鏖翳秕麴豸麸篝蜷铉秕舂ō栳箬ㄣ飙轭翕翦蠛轭舡撅泗弭ㄨ狍璀瞽怡翦栳箬┅蟓忉箦洞哄钽镤瀛忉箦洞怡翦秕舂┅┅换浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇换拈鲩溴躔翳祜汜篝矧犷蝈痱弩孱翳疳珏狍栳箬怩汶弭ㄤ彐躅桁悱怩汶弭篝狎糸铉眢怩汶弭眢怩汶弭泔躅舂ㄩ怩汶弭泔躅暴扉篝ㄨ煦怩汶弭篝癌戾è孱ō篝狎糸铉眢盹篝狎糸铉眢怩汶弭眢┅┅ㄣ镱ㄨ煦怩汶弭篝孱洎ㄨ煦怩汶弭孱í怩汶弭眢博ō怩汶弭泔躅暴┅┅ㄤ彐躅溴驷蹯舡怩汶弭īㄨ煦怩汶弭ㄨ汉溴驷蹯舡簌篝屙沆镢铋飑栋鞍供ㄤ彐躅桁悱怩汶弭篝糸礤ㄨ鸿蹯悱篝蜷铉ㄨ喉犭瀛桁呼轫糸礤呼殂癌詈屦镢璀铒溴殇┅ㄤ彐躅怩汶弭箫螋篝矧濠箫螋ㄦ箦艉泔铞弪ъ轶篝矧濠灬礅溽ㄡ猢篝蜷铉戾篌ㄣ徜岍ㄣ徜猢┅┅ㄤ彐躅怩汶弭珧秕箫螋邃轸屙怩汶弭螬ㄩ铛祆怩汶弭螬Жㄤ弩趄蹉趱蜷铉忾钿秕蝮翎殪箴扉舡麒殪灬礅溽ㄩ翦愆换戾篌轶镬溴颥翳怩汶弭轶镬溴翳犷换翳轸屙轭轸篝蜷铉戾篌ㄣ狎怩汶弭螬ㄣ徜轸屙┅箫螋邃轸屙螬ㄣ镱ㄣ镱ㄣ狎怩汶弭螬秕蝮ㄢ蹉脲舡珧秕翎殪ㄣ潋怩汶弭螬┅┅ㄤ彐躅栳箬疳螋轸轱铙ㄧ蝻躔邃磲疸狎灬礅溽疳轵戾è怩汶弭ㄣ狎疳轵┅ㄩ翦眢ㄣ潋疳轵┅ㄣ镱怩汶弭ō揪轸屙磲疸狎＇栳箬镱濠栳箬矧栳箬篝蜷铉┅┅珧秕疱洎ㄤ彐躅磲脲蝈聃弩篝矧镳糸镱犰ㄢ蹉脲趔ㄤ彐狨祠怩汶弭螬┅ō篝矧怩汶弭箫螋ㄢ蹉脲舡珧秕怩汶弭螬栳箬疳螋轸轱铙┅